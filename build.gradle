plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

group 'org.msyu.javautil'
version '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.testng:testng:6.9.+'
    testImplementation 'org.mockito:mockito-core:1.10.+'
}

sourceSets {
    test.java.srcDir('src/examples/java')
}

test {
    useTestNG()
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifactId 'exceptions'

            pom {
                name = 'msyu-javautil-exceptions'
                description = 'A library of utilities to facilitate correct Java exception handling'
                url = 'https://github.com/ypoluektovich/msyu-javautil-exceptions'

                licenses {
                    license {
                        name = 'Mozilla Public License Version 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }

                developers {
                    developer {
                        id = 'ypoluektovich'
                        name = 'Yanus Poluektovich'
                        email = 'ypoluektovich@gmail.com'
                        url = 'https://github.com/ypoluektovich'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/ypoluektovich/msyu-javautil-exceptions.git'
                    developerConnection = 'scm:git:https://github.com/ypoluektovich/msyu-javautil-exceptions.git'
                    url = 'https://github.com/ypoluektovich/msyu-javautil-exceptions'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'OSSRH'
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.main
}
